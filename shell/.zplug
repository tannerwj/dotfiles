#!/bin/zsh

source $ZPLUG_HOME/init.zsh

zplug zplug/zplug, hook-build:'zplug --self-manage' # so zplug can update itself
zplug davidparsson/zsh-pyenv-lazy # save ~.25 seconds and lazy load pyenv
zplug romkatv/powerlevel10k, as:theme, depth:1 # zsh theme
zplug zsh-users/zsh-autosuggestions # suggests commands as you type based on history and completions
zplug zdharma-continuum/fast-syntax-highlighting # feature rich syntax highlighting for Zsh
zplug djui/alias-tips # alerts if you could've used an alias
zplug lukechilds/zsh-nvm # installing, updating and loading nvm
zplug z-shell/zsh-navigation-tools # lots of stuff, I like history features
# zplug marlonrichert/zsh-autocomplete # disabled: prefer classic zsh completion

# completions
# Lazy kubectl completion to avoid startup cost
if (( $+commands[kubectl] )); then
  _kubectl_lazy_complete() {
    unset -f _kubectl_lazy_complete
    source <(kubectl completion zsh)
    _kubectl "$@"
  }
  compdef _kubectl_lazy_complete kubectl
fi
zplug greymd/docker-zsh-completion # completion for docker and docker-compose
zplug zpm-zsh/ssh # ssh host completion
zplug wintermi/zsh-brew # brew completions

# only check zcompdump once a day
autoload -Uz compinit
if [ $(date +'%j') != $(/usr/bin/stat -f '%Sm' -t '%j' ${ZDOTDIR:-$HOME}/.zcompdump) ]; then
  compinit
else
  compinit -C
fi

# Only check/install plugins once a week to speed startup
ZPLUG_CHECK_FILE="${ZDOTDIR:-$HOME}/.zplug_check"
if [[ ! -f "$ZPLUG_CHECK_FILE" ]] || (( $(date +%s) - $(/usr/bin/stat -f %m "$ZPLUG_CHECK_FILE") > 604800 )); then
  if ! zplug check; then
      zplug install
  fi
  : >| "$ZPLUG_CHECK_FILE"
fi

# source plugins and add commands to the PATH (defer heavy ones)
DEFERRED_ZPLUG_PLUGINS=(
  zdharma-continuum/fast-syntax-highlighting
  z-shell/zsh-navigation-tools
)

zplug load \
  zplug/zplug \
  davidparsson/zsh-pyenv-lazy \
  romkatv/powerlevel10k \
  zsh-users/zsh-autosuggestions \
  djui/alias-tips \
  lukechilds/zsh-nvm \
  greymd/docker-zsh-completion \
  zpm-zsh/ssh \
  wintermi/zsh-brew

autoload -Uz add-zsh-hook
__zplug_deferred_loaded=0
__zplug_load_deferred() {
  (( __zplug_deferred_loaded )) && return
  __zplug_deferred_loaded=1
  zplug load "${DEFERRED_ZPLUG_PLUGINS[@]}"
}
add-zsh-hook precmd __zplug_load_deferred
